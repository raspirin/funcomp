// statement
stmts  =  { stmt* }
stmt   =  { draw | rot | scale | origin }
draw   = !{ ^"for" ~ ident ~ ^"from" ~ expr ~ ^"to" ~ expr ~ ^"step" ~ expr ~ ^"draw" ~ "(" ~ expr ~ "," ~ expr ~ ")" ~ ";" }
rot    = !{ ^"rot" ~ ^"is" ~ expr ~ ";" }
scale  = !{ ^"scale" ~ ^"is" ~ expr ~ ";" }
origin = !{ ^"origin" ~ ^"is" ~ expr ~ ";" }

// expr
expr      = !{ factor ~ ((plus | minus) ~ factor)* }
factor    = !{ unary ~ ((asterisk | slash) ~ unary)* }
unary     = !{ (minus | plus) ~ unary | call | primary }
call      = !{ primary ~ "(" ~ arguments* ~ ","? ~ ")" }
primary   =  { number | grouping | ident }
arguments =  { expr ~ ("," ~ expr)* }
grouping  = !{ "(" ~ expr ~ ")" }

// operator
plus     = { "+" }
minus    = { "-" }
asterisk = { "*" }
slash    = { "/" }

// atomic
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
ident  = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

// whitespace
WHITESPACE = _{ " " | NEWLINE }
